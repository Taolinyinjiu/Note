# 示例代码分析

## WiFi_Basic 普通 WiFi 连接方式，指定 WiFi SSID 和密码

### mqtt_connect_with_access_token 
在该示例中，每个设备使用自己的 AccessToken 连接 ThingsCloud
```cpp
// 导入Thingscloud中的wifimanger和mqtt，分别用于连接wifi配网，连接mqtt服务器
#include <ThingsCloudWiFiManager.h>
#include <ThingsCloudMQTT.h>
// 设置wifi名称与密码
const char *ssid = "";
const char *password = "";
// 通过宏定义的形式传递mqtt设备参数
#define THINGSCLOUD_MQTT_HOST ""
#define THINGSCLOUD_DEVICE_ACCESS_TOKEN ""
#define THINGSCLOUD_PROJECT_KEY ""
// 创建类ThingsCloudMQTT的实例client ，传递参数是上面宏定义的三个mqtt设备参数
ThingsCloudMQTT client(
    THINGSCLOUD_MQTT_HOST,
    THINGSCLOUD_DEVICE_ACCESS_TOKEN,
    THINGSCLOUD_PROJECT_KEY);
// 设置 LED GPIO 引脚
const int LED_PIN = 3;
// 上报数据的间隔时间计时器
unsigned long timer1 = millis();
// 设置定时上报数据的时间间隔，单位是 ms。免费版项目请务必大于30秒，否则设备可能会被限连。
const int report_interval = 1000 * 60 * 5;
// 声明函数，该函数用于向服务器发送设备检测到的传感器值
void pubSensors();
// 初始化函数，设备上电后只会执行一次
void setup()
{
    // 初始化串口，可用typeC线同ESP32开发板连接，用作调试以及日志输出
    Serial.begin(115200);

    // 拉低 LED
    pinMode(LED_PIN, OUTPUT);   // 设置引脚模式为输出
    digitalWrite(LED_PIN, LOW);    // 设置引脚电平为低电平

    // 允许 SDK 的日志输出
    client.enableDebuggingMessages();

    // 连接 WiFi AP, 传入参数为wifi名与密码
    client.setWifiCredentials(ssid, password);
}
// 模拟检测到的传感器值，生成发送报文填充内容json字符串，将设备属性上传到thingscloud服务器
void pubSensors()
{
    // 创建一个 DynamicJsonDocument 类型的对象，命名为 obj
    // DynamicJsonDocument 是 ArduinoJson 库中的一个类，用于动态地分配内存来存储 JSON 数据
    // (512) 表示这个 JSON 文档预分配的内存大小为 512 字节。这可以避免在添加 JSON 元素时进行多次内存重新分配，提高效率。
    DynamicJsonDocument obj(512);
    // 向 JSON 对象 obj 中添加一个键值对,键为"temperature"，值为31.2
    obj["temperature"] = 31.2;
    obj["humidity"] = 62.5;
    obj["co2"] = 2321;
    obj["light"] = 653;
    // 声明了一个字符数组 attributes，大小为 512 字节
    char attributes[512];
    // 使用 ArduinoJson 库中的 serializeJson() 函数将 JSON 对象 obj 序列化（转换为字符串）并存储到字符数组 attributes 中
    serializeJson(obj, attributes);
    // 调用属性上报方法，传入参数为报文字符串
    client.reportAttributes(attributes);
}

// MQTT连接回调函数，该函数必须实现，否则在服务器连接成功后，客户端（ESP32）接收到服务器消息时找不到回调函数时设备将会进入未定义的状态， 表现为卡死或是其他异常行为
void onMQTTConnect()
{
    // 这里点亮 LED
    digitalWrite(LED_PIN, HIGH);

    // 订阅属性上报的回复消息，向调试串口打印回复报文
    // 使用client对象的onAttributesResponse函数，注册了一个匿名函数作为回调函数，接受参数为字符串
    client.onAttributesResponse([](const String &payload)
                                { Serial.println("attributes response: " + payload); });

    // 订阅属性获取的回复消息，向调试串口打印回复报文
    client.onAttributesGetResponse([](const String &topic, const String &payload)
                                   { Serial.println("attributes get response: " + topic + ", payload: " + payload); });

    // 订阅云平台下发属性的消息，向调试串口打印回复报文
    client.onAttributesPush([](const String &payload)
    {
        // 当云平台主动向设备下发属性更新时，服务器会通过 MQTT 发送一个消息。这个回调函数会在接收到这类下发消息时被执行。
        Serial.println("attributes push: " + payload);
        // 
        DynamicJsonDocument doc(512);
        // 使用 ArduinoJson 库将 payload 反序列化为一个 DynamicJsonDocument 对象 doc
        DeserializationError error = deserializeJson(doc, payload);
        // 如果反序列化过程中发生错误，会将错误信息打印到串口并返回，停止后续处理。
        if (error)
        {
            Serial.printf("deserialize error: %s\n", error.f_str());
            return;
        }
        // 将 doc 转换为一个 JsonObject 对象 obj
        JsonObject obj = doc.as<JsonObject>();
        // 检测obj对象中是否存在键"relay1"
        if (obj.containsKey("relay1"))
        {
            // 存在键，检测对应值
            if (obj["relay1"] == true)
            {
                Serial.println("relay1 ON");
                // todo 输出 GPIO 控制继电器
            }
            else
            {
                Serial.println("relay1 OFF");
                // todo 输出 GPIO 控制继电器
            }
        } 
    });

    // 读取设备在云平台上的属性
    client.getAttributes();

    // 延迟 5 秒上报首次传感器数据
    client.executeDelayed(1000 * 5, []()
                          { pubSensors(); });
}

// 程序主循环
void loop()
{
    // 向mqtt发送心跳包，保证设备在线，如果没有这行代码，则设备不向服务器发送心跳消息，在一段时间内没向服务器发送心跳包，则服务器认定设备离线
    client.loop();

    // 按间隔时间上报传感器数据，millis函数返回一个无符号长整型值(unsigned long)，表示自启动或复位后到现在时刻经历的毫秒数
    if (millis() - timer1 > report_interval)
    {
        timer1 = millis();
        pubSensors();
    }
}

```

### mqtt_connect_with_device_key
在该示例中，每个设备使用DeviceKey 请求 AccessToken，连接 ThingsCloud
由于代码同上一个示例重复太多，因此只简单挑出不同的地方进行注释
```cpp
// 该示例传入参数相对上个实例多了一个，主要用于一型一密连接方式，该连接方式适合同型号多传感器大规模部署
#define THINGSCLOUD_MQTT_HOST ""    // MQTT 服务器域名
#define THINGSCLOUD_PROJECT_KEY ""  // 同一个项目所有设备的 ProjectKey 相同
#define THINGSCLOUD_TYPE_KEY ""     // 设备类型的 TypeKey，用于自动创建设备
#define THINGSCLOUD_API_ENDPOINT "" // 用于获取设备证书以及自动创建设备

// 多态，通过传入参数不同，调用不同的类构造函数
ThingsCloudMQTT client(
    THINGSCLOUD_MQTT_HOST,
    "",               // DeviceKey 留空，SDK 自动为模组生成唯一标识作为 DeviceKey
    THINGSCLOUD_PROJECT_KEY,
    THINGSCLOUD_TYPE_KEY,
    THINGSCLOUD_API_ENDPOINT);

// 后面没有不同的代码了
```

### mqtt_connect_with_device_key_auto_create_device
在该实例中，每个设备使用 DeviceKey 请求 AccessToken，连接 ThingsCloud。如果 DeviceKey 的设备不存在，支持自动创建设备
```cpp
// 同上一个示例不同，本次类在构造时要求传入Device_key,如果Device_key对应的设备不存在，则自动创建设备，但该创建流程在mqtt服务器上完成，不在客户端进行修改，其他代码与第一个示例一样。
ThingsCloudMQTT client(
    THINGSCLOUD_MQTT_HOST,
    THINGSCLOUD_DEVICE_KEY,
    THINGSCLOUD_PROJECT_KEY,
    THINGSCLOUD_TYPE_KEY,
    THINGSCLOUD_API_ENDPOINT);

```

### mqtt_connect_with_auto_device_key 
每个设备自动生成唯一的 DeviceKey，请求 AccessToken，连接 ThingsCloud。
```cpp
#define THINGSCLOUD_MQTT_HOST ""    // MQTT 服务器域名
#define THINGSCLOUD_PROJECT_KEY ""  // 同一个项目所有设备的 ProjectKey 相同
#define THINGSCLOUD_TYPE_KEY ""     // 设备类型的 TypeKey，用于自动创建设备
#define THINGSCLOUD_API_ENDPOINT "" // 用于获取设备证书以及自动创建设备

ThingsCloudMQTT client(
    THINGSCLOUD_MQTT_HOST,
    "",               // DeviceKey 留空，SDK 自动为模组生成唯一标识作为 DeviceKey
    THINGSCLOUD_PROJECT_KEY,
    THINGSCLOUD_TYPE_KEY,
    THINGSCLOUD_API_ENDPOINT);

```


## WiFi_Provisioning_AP_Mode 源代码不指定wifi，使用ThingX App完成wifi配网

### wifi_provisioning_cliam_device
WiFi 配网模式的示例，用户完成配网后领取设备。
```cpp
// ESP模组生成 WiFi 热点，需要手机连接热点，然后打开ThingX app进行下一步的配置网络
#define WiFi_AP_SSID "ESP32_DEVICE" // AP 的 SSID 前缀，出现在用户 App 的 WiFi 列表中，可修改为你喜欢的名称
#define WiFi_AP_PASSWORD ""         // AP 的连接密码，可以不设置
// 构造类ThingsCloudWiFiManager的对象wm，传入参数为待创建热点的账号和密码
ThingsCloudWiFiManager wm(WiFi_AP_SSID, WiFi_AP_PASSWORD);
// 设备初始化流程
void setup()
{
    // ……同本次示例无关的代码已省略……

    // 关联 MQTT 客户端和配网管理器
    wm.linkMQTTClient(&client);
    // 实际产品中，将清空配网信息放在例如设备的按键事件中，由用户操作触发设备重新进入配网状态。
    // wm.resetSettings();

    // 如果设备未配网，则启动 AP 配网模式，等待 ThingsX App 为设备配网
    // 如果已配网，则直接连接 WiFi
    if (!wm.autoConnect())
    {
        Serial.println("\nWiFi provisioning failed, will restart to retry");
        delay(1000);
        ESP.restart();
    }
}
```

### wifi_provisioning_reset
通过长按按键重置配网的示例

```cpp
// 用于检测重置配网的按键，长按 2 秒
const int RESET_BUTTON_PIN = 4;
// 设置检测状态的初始值
int lastState = LOW;
// 设置按键现态的标志位
int currentState;

void setup()
{
    // 初始化配网重置按键，默认为下拉输入状态
    pinMode(RESET_BUTTON_PIN, INPUT_PULLDOWN);
}

// 检查当前重置按键状态
void checkResetButton()
{
    // 读取当前按键状态
    currentState = digitalRead(RESET_BUTTON_PIN);
    // 如果当前重置按键是高电平(按键被按下)，并且上一次是低电平
    if (currentState == HIGH && lastState == LOW)
    {
        // 更新时间戳
        pressedTime = millis();
        // 更新电平状态
        lastState = HIGH;
    }
    // 如果重置按键持续被按压，当前重置按键状态是高电平(按键被按下)，并且上一次也是高电平
    else if (currentState == HIGH && lastState == HIGH)
    {
        // r如果时间戳大于0并且时间差大于2s
        if (pressedTime > 0 && millis() - pressedTime > 2000)
        {
            // 检测到按键长按 2s，点亮 LED
            digitalWrite(LED_PIN, HIGH);
            Serial.println("\nWiFi reseting...");
            // 清空配网信息
            wm.resetSettings();
            delay(1000);
            // 重启设备，将重新进入等待配网状态
            Serial.println("\nESP restart...");
            ESP.restart();
        }
    }
    // 如果重置按键没有被按下
    else
    {
        // 清除时间戳
        pressedTime = 0;
        // 更新重置按键历史状态
        lastState = LOW;
    }
}
```

## MQTT_Communicate MQTT库通信示例

### mqtt_attributes
设备上报设备属性、接收云平台下发属性、读取云平台的设备属性。

该示例同第一个WIFI示例中代码一致，因此不做分析，后修代码看了一下也没有什么大的差异，示例分析到此结束